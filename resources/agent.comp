#version 430

#define texture_width 0
#define texture_height 0
#define l_size_x 1

#define PI 3.1415926535897932384626

layout(rgba32f, location=0) restrict uniform image2D texture;

layout(local_size_x=l_size_x, local_size_y=1, local_size_z=1) in;

struct Agent {
	vec2 pos;
	vec2 dir;
};

layout(std430, binding=0) buffer buf_ants {
	Agent agents[];
} Buf_agents;

uniform int nb_agent;

uniform float speed;
uniform float steerStrength;
uniform float wanderStrength;
uniform float sensorAngleSpacing;
uniform int sensorSize;
uniform int sensorDistance;

uniform int timer;

float map(float value, float min1, float max1, float min2, float max2) {
	return min2 + (value - min1) * (max2 - min2) / (max1 - min1);
}

uint hash(uint x) {
	x ^= 2747636419u;
	x *= 2654435769u;
	x ^= x >> 16;
	x *= 2654435769u;
	x ^= x >> 16;
	x *= 2654435769u;
	return x;
}

// uint hash(uint x) {
// 	x += ( x << 10u );
// 	x ^= ( x >>  6u );
// 	x += ( x <<  3u );
// 	x ^= ( x >> 11u );
// 	x += ( x << 15u );
// 	return x;
// }

float random_uniform(uint seed) {
	return float(hash(seed)) / 4294967295u;
}

vec2 random_uniform_vec2(uint seed) {
	const float angle = (random_uniform(seed) * 2*PI) - PI;
	return vec2(cos(angle), sin(angle));
}

float sense(Agent agent, float sensorAngleOffset) {
	float angle = atan(agent.dir.y, agent.dir.x);

	float sensorAngle = angle + sensorAngleOffset;
	vec2 sensorDir = vec2(cos(sensorAngle), sin(sensorAngle));
	ivec2 sensorCenter = ivec2(agent.pos + sensorDir * sensorDistance);

	float sum = 0.0;

	for (int offsetY = -sensorSize ; offsetY <= sensorSize ; ++offsetY) {
		for (int offsetX = -sensorSize ; offsetX <= sensorSize ; ++offsetX) {
			ivec2 pos = sensorCenter + ivec2(offsetX, offsetY);

			// check if pos is in texture bounds
			if (pos.x >= 0 && pos.x < texture_width && pos.y >= 0 && pos.y < texture_height)
				sum += imageLoad(texture, pos).r;
		}
	}
	return sum;
}

vec2 turn(vec2 dir, float angle) {
	const float newX = dir.x * cos(angle) - dir.y * sin(angle);
	const float newY = dir.x * sin(angle) + dir.y * cos(angle);
	return vec2(newX, newY);
}

void main()
{
	const uint id = gl_GlobalInvocationID.z * gl_NumWorkGroups.y * gl_NumWorkGroups.x +
					gl_GlobalInvocationID.y * gl_NumWorkGroups.x +
					gl_GlobalInvocationID.x;

	if (id >= nb_agent) return;

	const Agent agent = Buf_agents.agents[id];
	const uint seed = hash(timer + id);

	// steer base on sensory data //
	float weightForward = sense(agent, 0);
	float weightLeft = sense(agent, sensorAngleSpacing);
	float weightRight = sense(agent, -sensorAngleSpacing);

	const float randomSteerStrength = random_uniform(seed);

	// continue forward
	if (weightForward > weightLeft && weightForward > weightRight) {
		// do nothing
	}
	// turn randomly
	else if (weightForward < weightLeft && weightForward < weightRight) {
		const float angle = (randomSteerStrength - 0.5) * 2 * wanderStrength;
		Buf_agents.agents[id].dir = turn(agent.dir, angle);
	}
	// turn right
	else if (weightRight > weightLeft) {
		const float angle = randomSteerStrength * steerStrength;
		Buf_agents.agents[id].dir = turn(agent.dir, -angle);
	}
	// turn left
	else if (weightLeft > weightRight) {
		const float angle = randomSteerStrength * steerStrength;
		Buf_agents.agents[id].dir = turn(agent.dir, angle);
	}

	vec2 newPos = agent.pos + agent.dir * speed;
	// if agent get outside of map //
	if (newPos.x < 0 || newPos.x > texture_width || newPos.y < 0 || newPos.y > texture_height) {
		newPos = clamp(newPos, vec2(0.01, 0.01), vec2(texture_width - 0.01, texture_height - 0.01));
		Buf_agents.agents[id].dir = random_uniform_vec2(seed);
	}
	Buf_agents.agents[id].pos = newPos;

	// texture trail //
	int x = int(agent.pos.x);
	int y = int(agent.pos.y);

	ivec2 texelPos = ivec2(x, y);
	vec4 texel = vec4(imageLoad(texture, texelPos).rgba);
	vec4 newTexel = vec4(1, 0, 0, 1);
	imageStore(texture, texelPos, newTexel);
}
